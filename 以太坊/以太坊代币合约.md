# 以太坊代币合约

```
pragma solidity ^0.4.16;

interface tokenRecipient { function receiveApproval(address _from, uint256 _value, address _token, bytes _extraData) public; }

contract TokenERC20 {
    // 代币名
    string public name;
   // 代币logo
    string public symbol;
    // 代币小数位数
    uint8 public decimals = 18;
    // 18位小数是强烈建议的默认值，避免改变它
    uint256 public totalSupply;

    // 这将创建一个包含所有余额的数组
    mapping (address => uint256) public balanceOf;
    //某人授权给某人，使用自己的多少代币.
    //比如：当前调用者msg.sender，可以授权给很多地址他代币的使用权限。
    mapping (address => mapping (address => uint256)) public allowance;

    // 代币转移日志
    event Transfer(address indexed from, address indexed to, uint256 value);

    // 销毁代币日志
    event Burn(address indexed from, uint256 value);

    /**
     * 构造方法 
     */
    function TokenERC20(uint256 initialSupply,string tokenName,string tokenSymbol) public {
        //10 ** uint256(decimals)是10^18次方。
        totalSupply = initialSupply * 10 ** uint256(decimals);  //初始化代币总数  使用 decimal，两个星号**表示次方。
        balanceOf[msg.sender] = totalSupply;                // 给创建者所有的初始化代币
        name = tokenName;                                   // 设置代币显示的名字  
        symbol = tokenSymbol;                               //设置代币显示的符合，代币logo 
    }

    /**
     * 代币转移, internal只能合约内部调用
     */
    function _transfer(address _from, address _to, uint _value) internal {
        // 防止传输到0x0地址。 使用burn（）来代替
        require(_to != 0x0);
        // 检查发送方有足够代币
        require(balanceOf[_from] >= _value);
        // 防止溢出，超过uint的数据范围，会变为负数
        require(balanceOf[_to] + _value > balanceOf[_to]);
        // 保存以备将来的断言
        uint previousBalances = balanceOf[_from] + balanceOf[_to];
        // 发送方减掉代币
        balanceOf[_from] -= _value;
        // 接收方增加代币
        balanceOf[_to] += _value;
        Transfer(_from, _to, _value);//event 日志
        // 断言用于使用静态分析来查找代码中的bug。 他们永远不会失败
        assert(balanceOf[_from] + balanceOf[_to] == previousBalances);
    }

    /**
     * Transfer tokens
     *
     * @param _to 收币方地址
     * @param _value 转移多少代币
     */
    function transfer(address _to, uint256 _value) public {
        _transfer(msg.sender, _to, _value);
    }

    /**
     * 从其他地址转移代币，需要其他地址授权给调用的人。
     *
     */
    function transferFrom(address _from, address _to, uint256 _value) public returns (bool success) {
        require(_value <= allowance[_from][msg.sender]);     // 检查授权额度
        allowance[_from][msg.sender] -= _value;
        _transfer(_from, _to, _value);
        return true;
    }

    /**
     * 允许其他人，花费我的代币。
     *
     *授权给_spender地址，_value个代币
     *
     * @param _spender 授权给_spender地址
     * @param _value 代币数
     */
    function approve(address _spender, uint256 _value) public returns (bool success) {
        allowance[msg.sender][_spender] = _value;
        return true;
    }

    /**
     * Set allowance for other address and notify
     *
     * 允许 `_spender` 代表你花费不大于`_value` 个代币, and then ping the contract about it
     *
     * @param _spender 授权给哪个地址
     * @param _value 授权最大代币数量
     * @param _extraData 一些额外的信息发送到批准的合同
     */
    function approveAndCall(address _spender, uint256 _value, bytes _extraData)public returns (bool success) {
        tokenRecipient spender = tokenRecipient(_spender);
        if (approve(_spender, _value)) {
            spender.receiveApproval(msg.sender, _value, this, _extraData);
            return true;
        }
    }

    /**
     * 销毁代币 
     *
     * 从系统中不可逆地删除`_value'个代币
     */
    function burn(uint256 _value) public returns (bool success) {
        require(balanceOf[msg.sender] >= _value);   //检查调用者金额大于销毁数量
        balanceOf[msg.sender] -= _value;            // 调用者代币减掉
        totalSupply -= _value;                      // 总供应量减掉
        Burn(msg.sender, _value);                  // event日志
        return true;
    }

    /**
     * 从其他账户销毁代币
     *
     *
     * @param _from 从哪个账户销毁代币
     * @param _value 销毁的代币数
     */
    function burnFrom(address _from, uint256 _value) public returns (bool success) {
        require(balanceOf[_from] >= _value);                // Check if the targeted balance is enough
        require(_value <= allowance[_from][msg.sender]);    // Check allowance
        balanceOf[_from] -= _value;                         // Subtract from the targeted balance
        allowance[_from][msg.sender] -= _value;             // Subtract from the sender's allowance
        totalSupply -= _value;                              // 减掉总供应量
        Burn(_from, _value);                              // event日志
        return true;
    }
}
```


```
pragma solidity ^0.5.0;

// ----------------------------------------------------------------------------
// 'FIXED' 'Example Fixed Supply Token' token contract
//
// Symbol      : FIXED
// Name        : Example Fixed Supply Token
// Total supply: 1,000,000.000000000000000000
// Decimals    : 18
//
// Enjoy.
//
// (c) BokkyPooBah / Bok Consulting Pty Ltd 2018. The MIT Licence.
// ----------------------------------------------------------------------------


// ----------------------------------------------------------------------------
// Safe maths
// ----------------------------------------------------------------------------
library SafeMath {
    function add(uint a, uint b) internal pure returns (uint c) {
        c = a + b;
        require(c >= a);
    }
    function sub(uint a, uint b) internal pure returns (uint c) {
        require(b <= a);
        c = a - b;
    }
    function mul(uint a, uint b) internal pure returns (uint c) {
        c = a * b;
        require(a == 0 || c / a == b);
    }
    function div(uint a, uint b) internal pure returns (uint c) {
        require(b > 0);
        c = a / b;
    }
}


// ----------------------------------------------------------------------------
// ERC Token Standard #20 Interface
// https://github.com/ethereum/EIPs/blob/master/EIPS/eip-20.md
// ----------------------------------------------------------------------------
contract ERC20Interface {
    function totalSupply() public view returns (uint);
    function balanceOf(address tokenOwner) public view returns (uint balance);
    function allowance(address tokenOwner, address spender) public view returns (uint remaining);
    function transfer(address to, uint tokens) public returns (bool success);
    function approve(address spender, uint tokens) public returns (bool success);
    function transferFrom(address from, address to, uint tokens) public returns (bool success);

    event Transfer(address indexed from, address indexed to, uint tokens);
    event Approval(address indexed tokenOwner, address indexed spender, uint tokens);
}


// ----------------------------------------------------------------------------
// Contract function to receive approval and execute function in one call
//
// Borrowed from MiniMeToken
// ----------------------------------------------------------------------------
contract ApproveAndCallFallBack {
    function receiveApproval(address from, uint256 tokens, address token, bytes memory data) public;
}


// ----------------------------------------------------------------------------
// Owned contract
// ----------------------------------------------------------------------------
contract Owned {
    address public owner;
    address public newOwner;

    event OwnershipTransferred(address indexed _from, address indexed _to);

    constructor() public {
        owner = msg.sender;
    }

    modifier onlyOwner {
        require(msg.sender == owner);
        _;
    }

    function transferOwnership(address _newOwner) public onlyOwner {
        newOwner = _newOwner;
    }
    function acceptOwnership() public {
        require(msg.sender == newOwner);
        emit OwnershipTransferred(owner, newOwner);
        owner = newOwner;
        newOwner = address(0);
    }
}


// ----------------------------------------------------------------------------
// ERC20 Token, with the addition of symbol, name and decimals and a
// fixed supply
// ----------------------------------------------------------------------------
contract FixedSupplyToken is ERC20Interface, Owned {
    using SafeMath for uint;

    string public symbol;
    string public  name;
    uint8 public decimals;
    uint _totalSupply;

    mapping(address => uint) balances;
    mapping(address => mapping(address => uint)) allowed;


    // ------------------------------------------------------------------------
    // Constructor
    // ------------------------------------------------------------------------
    constructor() public {
        symbol = "FIXED";
        name = "Example Fixed Supply Token";
        decimals = 18;
        _totalSupply = 1000000 * 10**uint(decimals);
        balances[owner] = _totalSupply;
        emit Transfer(address(0), owner, _totalSupply);
    }


    // ------------------------------------------------------------------------
    // Total supply
    // ------------------------------------------------------------------------
    function totalSupply() public view returns (uint) {
        return _totalSupply.sub(balances[address(0)]);
    }


    // ------------------------------------------------------------------------
    // Get the token balance for account `tokenOwner`
    // ------------------------------------------------------------------------
    function balanceOf(address tokenOwner) public view returns (uint balance) {
        return balances[tokenOwner];
    }


    // ------------------------------------------------------------------------
    // Transfer the balance from token owner's account to `to` account
    // - Owner's account must have sufficient balance to transfer
    // - 0 value transfers are allowed
    // ------------------------------------------------------------------------
    function transfer(address to, uint tokens) public returns (bool success) {
        balances[msg.sender] = balances[msg.sender].sub(tokens);
        balances[to] = balances[to].add(tokens);
        emit Transfer(msg.sender, to, tokens);
        return true;
    }


    // ------------------------------------------------------------------------
    // Token owner can approve for `spender` to transferFrom(...) `tokens`
    // from the token owner's account
    //
    // https://github.com/ethereum/EIPs/blob/master/EIPS/eip-20-token-standard.md
    // recommends that there are no checks for the approval double-spend attack
    // as this should be implemented in user interfaces
    // ------------------------------------------------------------------------
    function approve(address spender, uint tokens) public returns (bool success) {
        allowed[msg.sender][spender] = tokens;
        emit Approval(msg.sender, spender, tokens);
        return true;
    }


    // ------------------------------------------------------------------------
    // Transfer `tokens` from the `from` account to the `to` account
    //
    // The calling account must already have sufficient tokens approve(...)-d
    // for spending from the `from` account and
    // - From account must have sufficient balance to transfer
    // - Spender must have sufficient allowance to transfer
    // - 0 value transfers are allowed
    // ------------------------------------------------------------------------
    function transferFrom(address from, address to, uint tokens) public returns (bool success) {
        balances[from] = balances[from].sub(tokens);
        allowed[from][msg.sender] = allowed[from][msg.sender].sub(tokens);
        balances[to] = balances[to].add(tokens);
        emit Transfer(from, to, tokens);
        return true;
    }


    // ------------------------------------------------------------------------
    // Returns the amount of tokens approved by the owner that can be
    // transferred to the spender's account
    // ------------------------------------------------------------------------
    function allowance(address tokenOwner, address spender) public view returns (uint remaining) {
        return allowed[tokenOwner][spender];
    }


    // ------------------------------------------------------------------------
    // Token owner can approve for `spender` to transferFrom(...) `tokens`
    // from the token owner's account. The `spender` contract function
    // `receiveApproval(...)` is then executed
    // ------------------------------------------------------------------------
    function approveAndCall(address spender, uint tokens, bytes memory data) public returns (bool success) {
        allowed[msg.sender][spender] = tokens;
        emit Approval(msg.sender, spender, tokens);
        ApproveAndCallFallBack(spender).receiveApproval(msg.sender, tokens, address(this), data);
        return true;
    }


    // ------------------------------------------------------------------------
    // Don't accept ETH
    // ------------------------------------------------------------------------
    function () external payable {
        revert();
    }


    // ------------------------------------------------------------------------
    // Owner can transfer out any accidentally sent ERC20 tokens
    // ------------------------------------------------------------------------
    function transferAnyERC20Token(address tokenAddress, uint tokens) public onlyOwner returns (bool success) {
        return ERC20Interface(tokenAddress).transfer(owner, tokens);
    }
}
```

## 参考文档

- [solidity的interface](https://my.oschina.net/u/2601303/blog/1550469)
- [更新Etherscan代币ICON](https://ethereum.stackexchange.com/questions/31902/how-to-verify-token-after-etherscan-and-submit-icon)
- [代币合约标准](https://theethereum.wiki/w/index.php/ERC20_Token_Standard)
- [比特派使用文档](http://docs.bitpie.com/zh_CN/latest/index.html)
- [教你如何一步步创建ERC20代币](https://blog.csdn.net/pony_maggie/article/details/79588259)
- [以太坊开发(二十二)发布代币以及将合约代码上传至Etherscan](https://www.jianshu.com/p/cb66b033c63f)
- [以太坊入门手册（附各类工具链接）](https://www.odaily.com/post/5135970)
- [手把手教你发行自己的以太坊ERC20 Token](https://www.jianshu.com/p/e6ca8122a3a3)
- [Etherscan API 中文文档](https://learnblockchain.cn/docs/etherscan/Introduction.html#id1)